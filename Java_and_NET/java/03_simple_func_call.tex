% TODO proof-reading
\section{\EN{Simple function calling}\RU{Простой вызов функций}}

\EN{\TT{Math.random()} returns a pseudorandom number in range of [0.0 \dots 1.0), but let's say,
by some reason, we need to devise a function returning number in range of [0.0 \dots 0.5):}
\RU{\TT{Math.random()} возвращает псевдослучайное число в пределах [0.0 \dots 1.0), но представим,
по какой-то причине, нам нужна функция, возвращающая число в пределах [0.0 \dots 0.5):}

\begin{lstlisting}
public class HalfRandom
{ 
	public static double f()
	{
		return Math.random()/2;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Methodref          #18.#19        //  java/lang/Math.random:()D
   #3 = Double             2.0d
...
  #12 = Utf8               ()D
...
  #18 = Class              #22            //  java/lang/Math
  #19 = NameAndType        #23:#12        //  random:()D
  #22 = Utf8               java/lang/Math
  #23 = Utf8               random
\end{lstlisting}

\begin{lstlisting}
  public static double f();
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=0, args_size=0
         0: invokestatic  #2                  // Method java/lang/Math.random:()D
         3: ldc2_w        #3                  // double 2.0d
         6: ddiv          
         7: dreturn       
\end{lstlisting}

\EN{\TT{invokestatic} calls for the \TT{Math.random()} function and left result at the \ac{TOS}.}
\RU{\TT{invokestatic} вызывает функцию \TT{Math.random()} и оставляет результат на \ac{TOS}.}
\EN{Then the result is divided by 2.0 and returned.}
\RU{Затем результат делится на 2.0 и возвращается.}
\EN{But how function name is encoded?}
\RU{Но как закодировано имя функции?}
\EN{It's encoded in the constant pool using \TT{Methodref} expression.}
\RU{Оно закодировано в пуле констант используя выражение \TT{Methodref}.}
\EN{It defines class and method names.}
\RU{Оно определяет имена класса и метода.}
\EN{First field of \TT{Methodref} is points to the \TT{Class} expression which is, in turn, points to
the usual text string (\q{java/lang/Math}).}
\RU{Первое поле \TT{Methodref} указывает на выражение \TT{Class}, которое, в свою очередь,
указывает на обычную текстовую строку (\q{java/lang/Math}).}
\EN{Second \TT{Methodref} expression points to \TT{NameAndType} expression which also 
has two links to the strings.}
\RU{Второе выражение \TT{Methodref} указывает на выражение \TT{NameAndType}, которое также
имеет две ссылки на строки.}
\EN{First string is \q{random} which is the name of method.}
\RU{Первая строка это \q{random}, это имя метода.}
\EN{The second string is \q{()D} which encodes function type.
It implies it returns \IT{double} value (hence \IT{D} in the string).}
\RU{Вторая строка это \q{()D}, которая кодирует тип функции. Это означает что возвращаемый тип\EMDASH{}\IT{double} (отсюда \IT{D} в строке).}
\EN{This is the way 
1) JVM can check data type correctness; 
2) Java decompilers can restore data types from a compiled class file.}
\RU{Благодаря этому
1) JVM проверяет корректность типов данных; 
2) Java-декомпиляторы могут восстанавливать типы данных из class-файлов.}

\EN{Now let's finally try \q{Hello, world!} example:}
\RU{Наконец попробуем пример \q{Hello, world!}:}

\begin{lstlisting}
public class HelloWorld
{
	public static void main(String[] args)
	{
		System.out.println("Hello, World");
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Fieldref           #16.#17        //  java/lang/System.out:Ljava/io/PrintStream;
   #3 = String             #18            //  Hello, World
   #4 = Methodref          #19.#20        //  java/io/PrintStream.println:(Ljava/lang/String;)V
...
  #16 = Class              #23            //  java/lang/System
  #17 = NameAndType        #24:#25        //  out:Ljava/io/PrintStream;
  #18 = Utf8               Hello, World
  #19 = Class              #26            //  java/io/PrintStream
  #20 = NameAndType        #27:#28        //  println:(Ljava/lang/String;)V
...
  #23 = Utf8               java/lang/System
  #24 = Utf8               out
  #25 = Utf8               Ljava/io/PrintStream;
  #26 = Utf8               java/io/PrintStream
  #27 = Utf8               println
  #28 = Utf8               (Ljava/lang/String;)V
...
\end{lstlisting}

\begin{lstlisting}
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
         3: ldc           #3                  // String Hello, World
         5: invokevirtual #4                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
         8: return        
\end{lstlisting}

\EN{\TT{ldc} at offset 3 is taking pointer to the \q{Hello, World} string in constant pool
and pushes into stack.}
\RU{\TT{ldc} по смещению 3 берет указатель (или адрес) на строку \q{Hello, World}
в пуле констант и заталкивает его в стек.}
\EN{It's called \IT{reference} in the Java world, but it's rather pointer, or address}
\RU{В мире Java это называется \IT{reference}, но это скорее указатель или просто адрес}
\footnote{\RU{О разнице между указателями и \IT{reference} в С++}\EN{About difference in pointers and \IT{reference}'s in C++}: \myref{cpp_references}.}.

\EN{The familiar \TT{invokevirtual} instruction takes information about \TT{println} 
function (or method) from the constant pool and call it.}
\RU{Уже знакомая нам инструкция \TT{invokevirtual} берет информацию о функции (или методе) \TT{println} 
из пула констант и вызывает её.}
\EN{As we may know, there are several \TT{println} methods, dedicated for each data type.}
\RU{Как мы можем знать, есть несколько методов \TT{println}, каждый предназначен для каждого типа
данных.}
\EN{Our case is \TT{println} function version, which is intended for \IT{String} data type.}
\RU{В нашем случае, используется та версия \TT{println}, которая для типа данных \IT{String}.}

\EN{But what about the first \TT{getstatic} instruction?}
\RU{Что насчет первой инструкции \TT{getstatic}?}
\EN{This instruction takes a \IT{reference} (or address of) field of object \TT{System.out} 
and pushes it into the stack.}
\RU{Эта инструкция берет \IT{reference} (или адрес) поля объекта \TT{System.out}
и заталкивает его в стек.}
\EN{This value is acting like \IT{this} pointer for \TT{println} method.}
\RU{Это значение работает как указатель \IT{this} для метода \TT{println}.}
\EN{Thus, internally, \TT{println} method takes two arguments at input:
1) \IT{this}, i.e., pointer to object; 
2) address of \q{Hello, World} string.}
\RU{Таким образом, внутри, метод \TT{println} берет на вход два аргумента:
1) \IT{this}, т.е. указатель на объект
\footnote{Или \q{экземпляр класса} в некоторой русскоязычной литературе.};
2) адрес строки \q{Hello, World}.}

\EN{Indeed, \TT{println()} is called as a method within initialized \TT{System.out} object.}
\RU{Действительно, \TT{println()} вызывается как метод в рамках инициализированного объекта 
\TT{System.out}.}

\EN{For convenience, \TT{javap} utility writes all this information in comments.}
\RU{Для удобства, утилита \TT{javap} пишет всю эту информацию в комментариях.}
